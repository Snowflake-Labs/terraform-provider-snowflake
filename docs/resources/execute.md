---
page_title: "snowflake_execute Resource - terraform-provider-snowflake"
subcategory: "Stable"
description: |-
  Resource allowing execution of ANY SQL statement.
---

# snowflake_execute (Resource)

!> **Warning** This is a dangerous resource that allows executing **ANY** SQL statement. It may destroy resources if used incorrectly. It may behave incorrectly combined with other resources. Use at your own risk.

!> **Sensitive values** This resource's `execute`, `query`, `revert` and `query_results` fields are not marked as sensitive in the provider. Ensure that no personal data, sensitive data, export-controlled data, or other regulated data is entered as metadata when using the provider. If you use one of these fields, they may be present in logs, so ensure that the provider logs are properly restricted. For more information, see [Sensitive values limitations](../#sensitive-values-limitations) and [Metadata fields in Snowflake](https://docs.snowflake.com/en/sql-reference/metadata).

~> **Note** It can be theoretically used to manage resource that are not supported by the provider. This is risky and may brake other resources if used incorrectly.

~> **Note** Use `query` parameter with caution. It will fetch **ALL** the results returned by the query provided. Try to limit the number of results by writing query with filters. Query failure does not stop resource creation; it simply results in `query_results` being empty.

-> **Note**: Default timeout is set to 60 minutes for each Terraform operation.

Resource allowing execution of ANY SQL statement.

## Example Usage

```terraform
##################################
### simple use cases
##################################

# create and destroy resource
resource "snowflake_execute" "test" {
  execute = "CREATE DATABASE ABC"
  revert  = "DROP DATABASE ABC"
}

# create and destroy resource using qualified name
resource "snowflake_execute" "test" {
  execute = "CREATE DATABASE \"abc\""
  revert  = "DROP DATABASE \"abc\""
}

# with query
resource "snowflake_execute" "test" {
  execute = "CREATE DATABASE ABC"
  revert  = "DROP DATABASE ABC"
  query   = "SHOW DATABASES LIKE '%ABC%'"
}

##################################
### grants example
##################################

# grant and revoke privilege USAGE to ROLE on database
resource "snowflake_execute" "test" {
  execute = "GRANT USAGE ON DATABASE ABC TO ROLE XYZ"
  revert  = "REVOKE USAGE ON DATABASE ABC FROM ROLE XYZ"
}

# grant and revoke with for_each
variable "database_grants" {
  type = list(object({
    database_name = string
    role_id       = string
    privileges    = list(string)
  }))
}

resource "snowflake_execute" "test" {
  for_each = { for index, db_grant in var.database_grants : index => db_grant }
  execute  = "GRANT ${join(",", each.value.privileges)} ON DATABASE ${each.value.database_name} TO ROLE ${each.value.role_id}"
  revert   = "REVOKE ${join(",", each.value.privileges)} ON DATABASE ${each.value.database_name} FROM ROLE ${each.value.role_id}"
}

##################################
### fixing bad configuration
##################################

# bad revert
# 1 - resource created with a bad revert; it is constructed, revert is not validated before destroy happens
resource "snowflake_execute" "test" {
  execute = "CREATE DATABASE ABC"
  revert  = "SELECT 1"
}

# 2 - fix the revert first; resource won't be recreated
resource "snowflake_execute" "test" {
  execute = "CREATE DATABASE ABC"
  revert  = "DROP DATABASE ABC"
}

# bad query
# 1 - resource will be created; query_results will be empty
resource "snowflake_execute" "test" {
  execute = "CREATE DATABASE ABC"
  revert  = "DROP DATABASE ABC"
  query   = "bad query"
}

# 2 - fix the query; query_results will be calculated; resource won't be recreated
resource "snowflake_execute" "test" {
  execute = "CREATE DATABASE ABC"
  revert  = "DROP DATABASE ABC"
  query   = "SHOW DATABASES LIKE '%ABC%'"
}

# timeouts
resource "snowflake_execute" "test" {
  execute = "CREATE DATABASE ABC"
  revert  = "DROP DATABASE ABC"
  query   = "SHOW DATABASES LIKE '%ABC%'"

  timeouts {
    create = "10m"
    read   = "10m"
    update = "10m"
    delete = "10m"
  }
}
```
-> **Note** Instead of using fully_qualified_name, you can reference objects managed outside Terraform by constructing a correct ID, consult [identifiers guide](../guides/identifiers_rework_design_decisions#new-computed-fully-qualified-name-field-in-resources).
<!-- TODO(SNOW-1634854): include an example showing both methods-->

-> **Note** If a field has a default value, it is shown next to the type in the schema.

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `execute` (String) SQL statement to execute. Forces recreation of resource on change.
- `revert` (String) SQL statement to revert the execute statement. Invoked when resource is being destroyed.

### Optional

- `query` (String) Optional SQL statement to do a read. Invoked on every resource refresh and every time it is changed.
- `timeouts` (Block, Optional) (see [below for nested schema](#nestedblock--timeouts))

### Read-Only

- `id` (String) The ID of this resource.
- `query_results` (List of Map of String) List of key-value maps (text to text) retrieved after executing read query. Will be empty if the query results in an error.

<a id="nestedblock--timeouts"></a>
### Nested Schema for `timeouts`

Optional:

- `create` (String)
- `delete` (String)
- `read` (String)
- `update` (String)

## Import

Import is supported using the following syntax:

```shell
terraform import snowflake_execute.example '<random_uuid>'
```
