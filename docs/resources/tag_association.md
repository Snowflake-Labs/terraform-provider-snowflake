---
page_title: "snowflake_tag_association Resource - terraform-provider-snowflake"
subcategory: ""
description: |-
  Resource used to manage tag associations. For more information, check object tagging documentation https://docs.snowflake.com/en/user-guide/object-tagging.
---

!> **V1 release candidate** This resource was reworked and is a release candidate for the V1. We do not expect significant changes in it before the V1. We will welcome any feedback and adjust the resource if needed. Any errors reported will be resolved with a higher priority. We encourage checking this resource out before the V1 release. Please follow the [migration guide](https://github.com/Snowflake-Labs/terraform-provider-snowflake/blob/main/MIGRATION_GUIDE.md#v0980--v0990) to use it.

-> **Note** For `ACCOUNT` object type, only identifiers with organization name are supported. See [account identifier docs](https://docs.snowflake.com/en/user-guide/admin-account-identifier#format-1-preferred-account-name-in-your-organization) for more details.

-> **Note** Tag association resource ID has the following format: `"TAG_DATABASE"."TAG_SCHEMA"."TAG_NAME"|TAG_VALUE|OBJECT_TYPE`. This means that a tuple of tag ID, tag value and object type should be unique across the resources. If you want to specify this combination for more than one object, you should use only one `tag_association` resource with specified `object_identifiers` set.

# snowflake_tag_association (Resource)

Resource used to manage tag associations. For more information, check [object tagging documentation](https://docs.snowflake.com/en/user-guide/object-tagging).

## Example Usage

```terraform
resource "snowflake_database" "test" {
  name = "database"
}

resource "snowflake_schema" "test" {
  name     = "schema"
  database = snowflake_database.test.name
}

resource "snowflake_tag" "test" {
  name           = "cost_center"
  database       = snowflake_database.test.name
  schema         = snowflake_schema.test.name
  allowed_values = ["finance", "engineering"]
}

resource "snowflake_tag_association" "db_association" {
  object_identifiers = [snowflake_database.test.fully_qualified_name]
  object_type        = "DATABASE"
  tag_id             = snowflake_tag.test.fully_qualified_name
  tag_value          = "finance"
}

resource "snowflake_table" "test" {
  database = snowflake_database.test.name
  schema   = snowflake_schema.test.name
  name     = "TABLE_NAME"
  comment  = "Terraform example table"
  column {
    name = "column1"
    type = "VARIANT"
  }
  column {
    name = "column2"
    type = "VARCHAR(16)"
  }
}

resource "snowflake_tag_association" "table_association" {
  object_identifiers = [snowflake_table.test.fully_qualified_name]
  object_type        = "TABLE"
  tag_id             = snowflake_tag.test.fully_qualified_name
  tag_value          = "engineering"
}

resource "snowflake_tag_association" "column_association" {
  object_identifiers = [snowflake_database.test.fully_qualified_name]
  object_type        = "COLUMN"
  tag_id             = snowflake_tag.test.fully_qualified_name
  tag_value          = "engineering"
}

resource "snowflake_tag_association" "account_association" {
  object_identifiers = ["\"ORGANIZATION_NAME\".\"ACCOUNT_NAME\""]
  object_type        = "ACCOUNT"
  tag_id             = snowflake_tag.test.fully_qualified_name
  tag_value          = "engineering"
}
```
-> **Note** Instead of using fully_qualified_name, you can reference objects managed outside Terraform by constructing a correct ID, consult [identifiers guide](https://registry.terraform.io/providers/Snowflake-Labs/snowflake/latest/docs/guides/identifiers#new-computed-fully-qualified-name-field-in-resources).
<!-- TODO(SNOW-1634854): include an example showing both methods-->

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `object_identifiers` (Set of String) Specifies the object identifiers for the tag association.
- `object_type` (String) Specifies the type of object to add a tag. Allowed object types: [ACCOUNT APPLICATION APPLICATION PACKAGE DATABASE FAILOVER GROUP INTEGRATION NETWORK POLICY REPLICATION GROUP ROLE SHARE USER WAREHOUSE DATABASE ROLE SCHEMA ALERT SNOWFLAKE.CORE.BUDGET SNOWFLAKE.ML.CLASSIFICATION EXTERNAL FUNCTION EXTERNAL TABLE FUNCTION GIT REPOSITORY ICEBERG TABLE MATERIALIZED VIEW PIPE MASKING POLICY PASSWORD POLICY ROW ACCESS POLICY SESSION POLICY PRIVACY POLICY PROCEDURE STAGE STREAM TABLE TASK VIEW COLUMN EVENT TABLE].
- `tag_id` (String) Specifies the identifier for the tag.
- `tag_value` (String) Specifies the value of the tag, (e.g. 'finance' or 'engineering')

### Optional

- `object_name` (String, Deprecated) Specifies the object identifier for the tag association.
- `skip_validation` (Boolean) If true, skips validation of the tag association.
- `timeouts` (Block, Optional) (see [below for nested schema](#nestedblock--timeouts))

### Read-Only

- `id` (String) The ID of this resource.

<a id="nestedblock--timeouts"></a>
### Nested Schema for `timeouts`

Optional:

- `create` (String)

## Import

~> **Note** Due to technical limitations of Terraform SDK, `object_identifiers` are not set during import state. Please run `terraform refresh` after importing to get this field populated.

Import is supported using the following syntax:

```shell
terraform import snowflake_tag_association.example '"TAG_DATABASE"."TAG_SCHEMA"."TAG_NAME"|TAG_VALUE|OBJECT_TYPE'
```
